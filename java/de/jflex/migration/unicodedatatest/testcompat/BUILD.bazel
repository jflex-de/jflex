load(":build_defs.bzl", "gen_test_compat")
load("//java/de/jflex/migration/unicodedatatest:build_defs.bzl", "TESTED_VERSIONS")

package(default_visibility = [
    "//javatests/de/jflex/migration/unicodedatatest/testcaseless:__pkg__",
    "//javatests/de/jflex/testcase/unicode:__subpackages__",
])

java_binary(
    name = "generator",
    testonly = True,
    srcs = ["CompatPropertiesTestGenerator.java"],
    main_class = "de.jflex.migration.unicodedatatest.testcompat.CompatPropertiesTestGenerator",
    deps = [
        ":testcompat",
        "//java/de/jflex/migration/unicodedatatest/base",
        "@maven//:org_apache_velocity_velocity",
    ],
)

java_library(
    name = "testcompat",
    testonly = True,
    srcs = glob(["Unicode*.java"]),
    resources = [
        "UnicodeCompat.flex.vm",
    ],
    deps = [
        "//java/de/jflex/migration/unicodedatatest/base",
        "//java/de/jflex/testing/unicodedata",
    ],
)

filegroup(
    name = "generate",
    testonly = True,
    srcs = [
        "gen_test_compat_%s_%s" % (x, y)
        for (x, y) in TESTED_VERSIONS
    ],
)

[gen_test_compat(
    name = "gen_test_compat_%s_%s" % (x, y),
    ucd = "//third_party/unicode:ucd_%s_%s" % (x, y),
    version = "%s.%s" % (x, y),
) for (x, y) in TESTED_VERSIONS]
